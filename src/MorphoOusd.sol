// SPDX-License-Identifier: AGPL-3.0
pragma solidity ^0.8.18;

import {UniswapV3Swapper} from "@periphery/swappers/UniswapV3Swapper.sol";
import {Base4626Compounder, ERC20, SafeERC20} from "@periphery/Bases/4626Compounder/Base4626Compounder.sol";
import {AuctionSwapper} from "@periphery/swappers/AuctionSwapper.sol";
import {IAuction} from "@periphery/interfaces/IAuction.sol";
import {IMetaMorpho, Id} from "./interfaces/Morpho/IMetaMorpho.sol";

contract MorphoOusd is Base4626Compounder, UniswapV3Swapper, AuctionSwapper {
    using SafeERC20 for ERC20;

    enum SwapType {
        NULL,
        UNISWAP_V3,
        AUCTION
    }

    // Mapping to be set by management for any reward tokens.
    // This can be used to set different mins for different tokens
    // or to set to uin256.max if selling a reward token is reverting
    mapping(address => uint256) public minAmountToSellMapping;

    mapping(address => SwapType) public swapType;

    address[] public allRewardTokens;

    Id public supplyMarketId;

    address public immutable OUSD;

    event SupplyMarketIdSet(Id indexed supplyMarketId);

    constructor(
        address _asset,
        string memory _name,
        address _vault,
        address _ousd
    ) Base4626Compounder(_asset, _name, _vault) {
        OUSD = _ousd;
    }

    // Override because we need to queue change to morpho vault to
    // This contract must have allocator role to change the vault queue
    function _deployFunds(uint256 _amount) internal virtual override {
        Id[] memory newSupplyQueue = new Id[](1);
        newSupplyQueue[0] = supplyMarketId;
        IMetaMorpho mmvault = IMetaMorpho(address(vault));
        mmvault.setSupplyQueue(newSupplyQueue);
        mmvault.deposit(_amount, address(this));
        mmvault.setSupplyQueue(new Id[](0));
    }

    function setSupplyMarketId(Id _supplyMarketId) external onlyManagement {
        supplyMarketId = _supplyMarketId;
        emit SupplyMarketIdSet(_supplyMarketId);
    }

    function availableDepositLimit(
        address _receiver
    ) public view override returns (uint256) {
        if (_receiver == OUSD) {
            return type(uint256).max;
        }
        return 0;
    }

    function addRewardToken(
        address _token,
        SwapType _swapType
    ) external onlyManagement {
        require(
            _token != address(asset) && _token != address(vault),
            "cannot be a reward token"
        );
        allRewardTokens.push(_token);
        swapType[_token] = _swapType;
    }

    function removeRewardToken(address _token) external onlyManagement {
        address[] memory _allRewardTokens = allRewardTokens;
        uint256 _length = _allRewardTokens.length;

        for (uint256 i = 0; i < _length; i++) {
            if (_allRewardTokens[i] == _token) {
                allRewardTokens[i] = _allRewardTokens[_length - 1];
                allRewardTokens.pop();
                break;
            }
        }
        delete swapType[_token];
    }

    function getAllRewardTokens() external view returns (address[] memory) {
        return allRewardTokens;
    }

    function setAuction(address _auction) external onlyManagement {
        require(IAuction(_auction).want() == address(asset), "wrong want");
        _setAuction(_auction);
    }

    function setUseAuction(bool _useAuction) external onlyManagement {
        _setUseAuction(_useAuction);
    }

    function setUniFees(
        address _token0,
        address _token1,
        uint24 _fee
    ) external onlyManagement {
        _setUniFees(_token0, _token1, _fee);
    }

    /**
     * @notice Set the swap type for a specific token.
     * @param _from The address of the token to set the swap type for.
     * @param _swapType The swap type to set.
     */
    function setSwapType(
        address _from,
        SwapType _swapType
    ) external onlyManagement {
        swapType[_from] = _swapType;
    }

    /**
     * @notice Set the `minAmountToSellMapping` for a specific `_token`.
     * @dev This can be used by management to adjust wether or not the
     * _claimAndSellRewards() function will attempt to sell a specific
     * reward token. This can be used if liquidity is to low, amounts
     * are to low or any other reason that may cause reverts.
     *
     * @param _token The address of the token to adjust.
     * @param _amount Min required amount to sell.
     */
    function setMinAmountToSellMapping(
        address _token,
        uint256 _amount
    ) external onlyManagement {
        minAmountToSellMapping[_token] = _amount;
    }

    function _claimAndSellRewards() internal override {
        address[] memory _allRewardTokens = allRewardTokens;
        uint256 _length = _allRewardTokens.length;

        for (uint256 i = 0; i < _length; i++) {
            address token = _allRewardTokens[i];
            SwapType _swapType = swapType[token];
            uint256 balance = ERC20(token).balanceOf(address(this));

            if (balance > minAmountToSellMapping[token]) {
                if (_swapType == SwapType.UNISWAP_V3) {
                    _swapFrom(token, address(asset), balance, 0);
                }
            }
        }
    }

    function kickAuction(address _token) external override returns (uint256) {
        require(swapType[_token] == SwapType.AUCTION, "!auction");
        require(
            _token != address(asset) && _token != address(vault),
            "cannot kick"
        );
        return _kickAuction(_token);
    }
}
